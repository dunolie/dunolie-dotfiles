#!/usr/bin/perl
#
#	git-森林
#	text-based tree visualisation
#	Copyright © Jan Engelhardt <jengelh [at] gmx de>, 2008
#
#	This program is free software; you can redistribute it and/or modify
#	it under the terms of the GNU General Public License as published by
#	the Free Software Foundation; either version 2 or 3 of the license.
#
use Getopt::Long;
use Git;
use strict;
use encoding "utf8";
my $Repo     = Git->repository($ENV{"GIT_DIR"} || ".");
my $Pretty_fmt  = "format:%s";
my $Show_all = 0;
my $Show_rebase = 1;
my $Style    = 2;
my $With_sha = 0;
my %Color    = (
	"default" => "\e[0m",
	"at"      => "\e[1;30m",
	"hhead"   => "\e[1;31m",
	"head"    => "\e[1;32m",
	"ref"     => "\e[1;34m",
	"remote"  => "\e[1;35m",
	"sha"     => "\e[0;31m",
	"tag"     => "\e[1;33m",
	"tree"    => "\e[0;33m",
);

&main();

sub main
{
	my $reverse_order = 0;

	&Getopt::Long::Configure(qw(bundling pass_through));
	&GetOptions(
		"all"      => \$Show_all,
		"no-color" => sub { %Color = (); },
		"no-rebase" => sub { $Show_rebase = 0; },
		"pretty=s"  => \$Pretty_fmt,
		"reverse"  => \$reverse_order,
		"style=i"  => \$Style,
		"sha"      => \$With_sha,
	);
	if (substr($Pretty_fmt, 0, 7) ne "format:") {
		die "If you use --pretty, it must be in the form of --pretty=format:";
	}
	$Pretty_fmt = substr($Pretty_fmt, 7);
	while ($Pretty_fmt =~ /\%./g) {
		if ($& eq "\%b" || $& eq "\%n") {
			die "Cannot use \%b or \%n in --pretty=format:";
		}
	}
	if ($Show_all) {
		#
		# Give --all back. And add HEAD to include commits
		# in the rev list that belong to a detached HEAD.
		#
		unshift(@ARGV, "--all", "HEAD");
	}
	if ($reverse_order) {
		tie(*STDOUT, "ReverseOutput");
	}
	&process();
	if ($reverse_order) {
		untie *STDOUT;
	}
}

sub process
{
	my(@vine);
	my $refs = &get_refs();
	my($fh, $fhc) = $Repo->command_output_pipe("log", "--topo-order",
	                "--pretty=format:<%H><%h><%P>$Pretty_fmt", @ARGV);

	while (defined(my $line = <$fh>)) {
		chomp $line;
		my($sha, $mini_sha, $parents, $msg) =
			($line =~ /^<(.*?)><(.*?)><(.*?)>(.*)/s);
		my @parents = split(" ", $parents);

		&vine_branch(\@vine, $sha);
		my $ra = &vine_commit(\@vine, $sha, \@parents);

		if (exists($refs->{$sha})) {
			print &vis_post(&vis_commit($ra,
			      $Color{at}."─".$Color{default}));
			&ref_print($refs->{$sha});
		} else {
			print &vis_post(&vis_commit($ra, " "));
		}
		if ($With_sha) {
			print $msg, $Color{at}, "──(", $Color{sha}, $mini_sha,
			      $Color{at}, ")", $Color{default}, "\n";
		} else {
			print $msg, "\n";
		}

		&vine_merge(\@vine, $sha, \@parents);
	}
	$Repo->command_close_pipe($fh, $fhc);
}

sub get_refs
{
	my($fh, $c) = $Repo->command_output_pipe("show-ref");
	my $ret = {};

	while (defined(my $ln = <$fh>)) {
		chomp $ln;
		if (length($ln) == 0) {
			next;
		}

		my($sha, $name) = ($ln =~ /^(\S+)\s+(.*)/s);
		if (!exists($ret->{$sha})) {
			$ret->{$sha} = [];
		}
		push(@{$ret->{$sha}}, $name);
		if ($name =~ m{^refs/tags/}) {
			my $sub_sha = $Repo->command("log", "-1",
			              "--pretty=format:%H", $name);
			chomp $sub_sha;
			if ($sha ne $sub_sha) {
				push(@{$ret->{$sub_sha}}, $name);
			}
		}
	}

	$Repo->command_close_pipe($fh, $c);

	my $rebase = -e $Repo->repo_path()."/.dotest-merge/git-rebase-todo" &&
	             $Show_rebase;
	if ($rebase) {
		if (open(my $act_fh, $Repo->repo_path().
		    "/.dotest-merge/git-rebase-todo")) {
			my($curr) = (<$act_fh> =~ /^\S+\s+(\S+)/);
			$curr = $Repo->command("rev-parse", $curr);
			chomp $curr;
			unshift(@{$ret->{$curr}}, "rebase/current");
			close $act_fh;
		}

		chomp(my $up   = $Repo->command("rev-parse", ".dotest-merge/upstream"));
		chomp(my $onto = $Repo->command("rev-parse", ".dotest-merge/onto"));
		chomp(my $old  = $Repo->command("rev-parse", ".dotest-merge/head"));
		unshift(@{$ret->{$up}}, "rebase/upstream");
		unshift(@{$ret->{$onto}}, "rebase/onto");
		unshift(@{$ret->{$old}}, "rebase/saved-HEAD");
	}

	my $head = $Repo->command("rev-parse", "HEAD");
	chomp $head;
	if ($rebase) {
		unshift(@{$ret->{$head}}, "rebase/new");
	}
	unshift(@{$ret->{$head}}, "HEAD");

	return $ret;
}

#
# ref_print - print a ref with color
# @s:	ref name
#
sub ref_print
{
	foreach my $symbol (@{shift @_}) {
		print $Color{at}, "[";
		if ($symbol eq "HEAD" || $symbol =~ m{^rebase/}) {
			print $Color{hhead}, $symbol;
		} elsif ($symbol =~ m{^refs/(remotes/[^/]+)/(.*)}s) {
			print $Color{remote}, $1, $Color{head}, "/$2";
		} elsif ($symbol =~ m{^refs/heads/(.*)}s) {
			print $Color{head}, $1;
		} elsif ($symbol =~ m{^refs/tags/(.*)}s) {
			print $Color{tag}, $1;
		} elsif ($symbol =~ m{^refs/(.*)}s) {
			print $Color{ref}, $1;
		}
		print $Color{at}, "]──", $Color{default};
	}
}

#
# vine_branch -
# @vine:	column array containing the expected parent IDs
# @rev:		commit ID
#
# Draws the branching vine matrix between a commit K and K^ (@rev).
#
sub vine_branch
{
	my($vine, $rev) = @_;
	my $idx;

	my $left = "╠";
	my $matched = 0;
	my $ret;

	# Transform array into string
	for ($idx = 0; $idx < scalar(@$vine); ++$idx) {
		if (!defined($vine->[$idx])) {
			$ret .= "═";
			next;
		} elsif ($vine->[$idx] ne $rev) {
			$ret .= "╪";
			next;
		}
		if ($matched == 0) {
			$ret .= "╠";
		} else {
			$ret .= "╩";
			$vine->[$idx] = undef;
		}
		++$matched;
	}

	if ($matched < 2) {
		return;
	}

	&remove_trailing_blanks($vine);
	print &vis_post(&vis_branch($ret)), "\n";
}

#
# vine_commit -
# @vine:	column array containing the expected IDs
# @rev:		commit ID
# @parents:	array of parent IDs
#
sub vine_commit
{
	my($vine, $rev, $parents) = @_;
	my $ret;

	for (my $i = 0; $i <= $#$vine; ++$i) {
		if (!defined($vine->[$i])) {
			$ret .= " ";
		} elsif ($vine->[$i] eq $rev) {
			$ret .= "╟";
		} else {
			$ret .= "║";
		}
	}

	if ($ret !~ /╟/) {
		# Not having produced a ╟ before means this is a tip
		my $i;
		for ($i = $#$vine; $i >= 0; --$i) {
			if (substr($ret, $i, 1) eq " ") {
				substr($ret, $i, 1) = "╓";
				$vine->[$i] = $rev;
				last;
			}
		}
		if ($i < 0) {
			$ret .= "╓";
			push(@$vine, $rev);
		}
	}

	&remove_trailing_blanks($vine);

	if (scalar(@$parents) == 0) {
		# tree root
		$ret =~ s/╟/╙/g;
	}

	return $ret;
}

#
# vine_merge -
# @vine:	column array containing the expected parent IDs
# @rev:		commit ID
# @parents:	parent IDs of @rev
#
# Draws the merging vine matrix between a commit K (@rev) and K^ (@parents).
#
sub vine_merge
{
	my($vine, $rev, $parents) = @_;
	my $orig_vine = -1;
	my @slot;
	my($ret, $max);

	for (my $i = 0; $i <= $#$vine; ++$i) {
		if ($vine->[$i] eq $rev) {
			$orig_vine = $i;
			last;
		}
	}

	if ($orig_vine == -1) {
		die "vine_commit() did not add this vine.";
	}

	if (scalar(@$parents) <= 1) {
		#
		# A single parent does not need a visual. Update and return.
		#
		$vine->[$orig_vine] = $parents->[0];
		&remove_trailing_blanks($vine);
		return;
	}

	#
	# Find some good spots to split out into and record columns
	# that will be used soon in the @slot list.
	#
	push(@slot, $orig_vine);
	my $parent = 0;

	for (my $seeker = 2; $parent < $#$parents &&
	    $seeker < 2 + 2 * $#$vine; ++$seeker)
	{
		my $idx = ($seeker % 2 == 0) ? -1 : 1;
		$idx   *= int($seeker / 2);
		$idx   += $orig_vine;

		if ($idx >= 0 && $idx <= $#$vine && !defined($vine->[$idx])) {
			push(@slot, $idx);
			$vine->[$idx] = "0" x 40;
			++$parent;
		}
	}
	for (my $idx = $orig_vine + 1; $parent < $#$parents; ++$idx) {
		if (!defined($vine->[$idx])) {
			push(@slot, $idx);
			++$parent;
		}
	}

	if (scalar(@slot) != scalar(@$parents)) {
		die "Serious internal problem";
	}

	@slot = sort { $a <=> $b } @slot;
	$max  = scalar(@$vine) + scalar(@slot);

	for (my $i = 0; $i < $max; ++$i) {
		if ($#slot >= 0 && $i == $slot[0]) {
			shift @slot;
			$vine->[$i] = shift @$parents;
			$ret .= ($i == $orig_vine) ? "S" : "s";
		} elsif (defined($vine->[$i])) {
			$ret .= "║";
		} else {
			$ret .= " ";
		}

	}

	$ret =~ s/ +$//gs;
	print &vis_post(&vis_merge($ret)), "\n";
}

#
# vis_* - transform control string into usable graphic
#
# To cut down on code, the three vine_* functions produce only a dumb,
# but already unambiguous, control string which needs some processing
# before it is ready for public display.
#


sub vis_branch
{
	# Sample input: ╬═╠╬╬╬╩╬═╬╬╬╬╬╬╩╬╩═╬╬
	my $ra = shift @_;
	my $i;

	$ra =~ s{^(.+?)╠}{
		$_ = $1;
		$_ =~ tr/╪═/║ /;
		$_ =~ s/(.)/$1 /gs;
		$_ .= '╠';
	}es;
	$ra =~ s{(╠.*)╩}{
		$_ = $1;
		$_ =~ s/(.)/$1═/gs;
		$_ .= '╝';
	}es;
	$ra =~ s{╝(.*)$}{
		$_ = $1;
		$_ =~ tr/╪═/║ /;
		$_ =~ s/(.)/$1 /gs;
		$_ = "╝ $_";
	}es;
	return $ra;
}

sub vis_commit
{
	my($ra, $sep) = @_;
	my($l, $r) = ($ra =~ /^(.*?)([╟╓╙].*)/);
	$l =~ s/(.)/$1 /gs;
	$r =~ s/(.)/$1 /gs;
	$r =~ s/ /$sep/gs;
	return $l.$r;
}

sub vis_merge
{
	my $s = shift @_;

	if ($s =~ s/(s.*)S(.*s)/&vis_merge3($1, $2)/es) {
		;
	} elsif ($s =~ /(?:s.*)S/s) {
		while ($s =~ s/(s.*)║(.*S)/$1╪$2/s) {
			;
		}
		$s =~ s/(s.*)S/&vis_merge2L($1)."╣"/es;
	} elsif ($s =~ /S(?:.*s)/s) {
		while ($s =~ s/(S.*)║(.*s)/$1╪$2/s) {
			;
		}
		$s =~ /S(.*s)/;
		$s =~ s/S(.*s)/"╠".&vis_merge2R($1)/es;
	} else {
		# $s =~ s/S/║/s;
		die "Should not come here";
	}
	$s =~ s{(.)}{&vis_merge1($1)}egs;
	return $s;
}

sub vis_merge1
{
	if ($_[0] eq "╔" || $_[0] eq "╦" || $_[0] eq "╠" || $_[0] eq "╪") {
		return $_[0]."═";
	} else {
		return $_[0]." ";
	}
}

sub vis_merge2L
{
	my $l = shift @_;
	$l =~ s/^s/╔/;
	$l =~ s/s/╦/g;
	return $l;
}

sub vis_merge2R
{
	my $r = shift @_;
	$r =~ s/s$/╗/;
	$r =~ s/s/╦/g;
	return $r;
}

sub vis_merge3
{
	my($l, $r) = shift @_;
	$l =~ s/^s/╔/;
	$l =~ s/s/╦/g;
	$r =~ s/s$/╗/;
	$r =~ s/s/╦/g;
	return "$l╪$r";
}

#
# vis_post - post-process/transform vine graphic
# Apply user-specific style transformation.
#
sub vis_post
{
	my $s = shift @_;

	if ($Style == 1) {
		$s =~ tr/╔╦╗╠╬╣╚╩╝║═╟╓╙╪/┌┬┐├┼┤└┴┘│─├┌└┼/;
	} elsif ($Style == 2) {
		$s =~ tr/╪/╬/;
	} elsif ($Style == 3) {
		$s =~ tr/╔╦╗╠╬╣╚╩╝║╟╓╙/╒╤╕╞╪╡╘╧╛│├┌└/;
	} elsif ($Style == 4) {
		$s =~ tr/╔╦╗╠╬╣╚╩╝╪═/╓╥╖╟╫╢╙╨╜╫─/;
	}

	if ($Color{default} ne "") {
		$s =~ s{\Q$Color{default}\E}{$&$Color{tree}}g;
	}
	return $Color{tree}, $s, $Color{default};
}

sub remove_trailing_blanks
{
	my $a = shift @_;

	while (scalar(@$a) > 0 && !defined($a->[$#$a])) {
		pop(@$a);
	}
}


package ReverseOutput;
require Tie::Handle;
@ReverseOutput::ISA = qw(Tie::Handle);

sub TIEHANDLE
{
	my $class = shift @_;
	my $self  = {};

	open($self->{fh}, ">&STDOUT");
	binmode $self->{fh}, ":utf8";

	return bless $self, $class;
}

sub PRINT
{
	my $self = shift @_;
	my $fh   = $self->{fh};

	$self->{saved} .= join($\, @_);
}

sub UNTIE
{
	my $self = shift @_;
	my $fh   = $self->{fh};

	$self->{saved} =~ tr/╔╦╗╒╤╕╓╥╖┌┬┐╚╩╝╘╧╛╙╨╜└┴┘╓╙/╚╩╝╘╧╛╙╨╜└┴┘╔╦╗╒╤╕╓╥╖┌┬┐╙╓/;
	print $fh join($/, reverse split(/\n/s, $self->{saved}));
	undef $self->{saved};
}
